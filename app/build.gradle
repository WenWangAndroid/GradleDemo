apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("local.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            keyAlias 'gradle_jks'
            keyPassword '123456'
            storeFile file('../jks/gradle.jks')
            storePassword '123456'
        }

        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true                      // 启用代码混淆
            shrinkResources true                    // 移除未使用的资源
            signingConfig signingConfigs.release    //配置签名
            // 混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "company"
    productFlavors {
        customer {
            dimension = "company"
            applicationIdSuffix ".customer"
        }
        fenrir {
            dimension = "company"
            applicationIdSuffix ".fenrir"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("fenrir") && variant.buildType.name == "release") {
            setIgnore(true)
        }
    }

//    splits {
//        density {           // 根据屏幕密度配置
//            enable true
//            exclude "ldpi", "xxhdpi", "xxxhdpi"                         //屏幕密度
//            compatibleScreens 'small', 'normal', 'large', 'xlarge'      //屏幕尺寸
//        }
//
//        abi {               // 手机架构
//            enable true
//            reset()
//            include "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
//            universalApk false
//        }
//    }

    //遍历 变体，批量修改APK 名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${variant.flavorName}_${variant.buildType.name}_${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${rootProject.ext.kotlin_version}"
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
